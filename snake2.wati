inclue "./lib/std.wati";

classe game contient
    methode constructeur () fait
        self.map = list();


        pour y allant de 0 a 10 fait
            p = list();
            pour x allant de 0 a 10 fait 
                si !et(y == 5, x == 6) alors
                    !p.plus("#")
                sinon
                    !p.plus(".")
                fin
            fin
            !self.map.plus(p)
        fin 

        self.x = 6;
        self.y = 5;

        self.queueX = list();
        self.queueY = list();

        self.pommeX = 0;
        self.pommeY = 0;

        self.taille = 1;

        self.r = random();

        self.head = terminal_bold + terminal_red + "o" + terminal_reset;
        self.blank = ".";
        self.queue = "#";
        self.pomme = "@";
        !self.ecrit(0, 0, self.pomme)
    fin

    methode print () fait
        pour y allant de 0 a 10 fait
            p = self.map[y];
            pour x allant de 0 a 10 fait 
                !print(p[x], " ")
            fin
            !print("\n")
        fin
    fin

    methode ecrit (x, y, char) fait
        p = self.map[y];
        !p.modifie(x, char)
    fin

    methode avance_queue () fait
        !self.ecrit(self.x, self.y, self.queue)
        !self.queueX.debut(self.x)
        !self.queueY.debut(self.y)
        si self.taille == self.queueX.taille alors
            px = !self.queueX.pop(self.queueX.taille - 1);
            py = !self.queueY.pop(self.queueY.taille - 1);
            !self.ecrit(px, py, self.blank)
        fin
    fin

    methode haut () fait
        !self.avance_queue()
        self.y = self.y - 1 % 10;
        si (self.y < 0) alors
            self.y = 10 + self.y;
        fin
        !self.ecrit(self.x, self.y, self.head)
    fin

    methode droite () fait
        !self.avance_queue()
        self.x = self.x + 1 % 10;
        !self.ecrit(self.x, self.y, self.head)
    fin

    methode gauche () fait
        !self.avance_queue()
        self.x = self.x - 1 % 10;
        si (self.x < 0) alors
            self.x = 10 + self.x;
        fin
        !self.ecrit(self.x, self.y, self.head)
    fin
    
    methode bas () fait
        !self.avance_queue()
        self.y = self.y + 1 % 10;
        !self.ecrit(self.x, self.y, self.head)
    fin

    methode check_collision () fait
        pour i allant de 0 a self.queueX.taille fait
            px = self.queueX[i];
            py = self.queueY[i];
            si !et(self.x == px, self.y == py) alors
                renvoie "collision";
            fin
        fin
        si !et(self.x == self.pommeX, self.y == self.pommeY) alors
            self.taille = self.taille + 1;
            !self.genere_pomme()
        fin
    fin

    methode genere_pomme () fait
        x = !self.r.rand(10);
        y = !self.r.rand(10);
        
        !self.ecrit(x, y, self.pomme)

        self.pommeX = x;
        self.pommeY = y;

    fin

    methode intro () fait
        !println("Commandes :")
        !println("z : haut")
        !println("s : bas")
        !println("d : droite")
        !println("q : gauche")
        !input("")
        !system("clear")
    fin

    methode boucle () fait
        !system("stty -icanon")
        run = "run";
        tant que run == "run" fait
            !self.print()
            !print("Score : ", self.taille)
            action = !input_cachÃ©("", "");
            

            si action == "z" alors
                !self.haut()
            fin
            si action == "s" alors
                !self.bas()
            fin
            si action == "q" alors
                !self.gauche()
            fin
            si action == "d" alors
                !self.droite()
            fin
            
            r = !self.check_collision();
            
            si r == "collision" alors
                run = "";
            fin

            !system("clear")
        fin
        !println("Perdu !")
    fin

fin

g = game();
!g.intro()
!g.boucle()