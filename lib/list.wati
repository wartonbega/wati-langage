inclue "node.wati";

classe list contient 
    methode constructeur () fait
        self.head = node("debut-de-liste");
        self.tail = node("fin-de-liste");
        self.head.next = self.tail;
        self.tail.prev = self.head;

        self.taille = 0;
    fin

    methode plus(content) fait
        n = node(content);
        n.next = self.tail;
        
        prev = self.tail.prev;
        n.prev = prev;
        prev.next = n;
        self.tail.prev = n;
        
        self.taille = self.taille + 1;
        renvoie self;
    fin

    methode debut(content) fait
        n = node(content);

        r = self.head.next;
        r.prev = n;
        n.next = r;
        self.head.next = n;
        n.prev = self.head;
        self.taille = self.taille + 1;
        renvoie self;
    fin
    
    methode modifie (index, content) fait
        i = 0;
        n = self.head.next;
        tant que (i != index) fait
            n = n.next;
            i = i + 1;
        fin
        n.content = content;
    fin

    methode print() fait
        (!print("[ "))
        n = self.head.next;
        tant que n.next.content != self.tail.content fait
            (!print(n.content, ", "))
            n = n.next;
        fin
        (!print(n.content, " ]\n"))
    fin

    methode en (x) fait
        i = 0;
        n = self.head.next;
        tant que i != x fait
            n = n.next;
            i = i + 1;
        fin
        p = n.content;

        renvoie p;
    fin

    methode pop (x) fait
        i = 0;
        n = self.head.next;
        tant que i != x fait
            n = n.next;
            i = i + 1;
        fin
        p = n.prev;
        next = n.next;
        p.next = next;
        next.prev = p;
        self.taille = self.taille - 1;
        libere n;
    fin


    methode envers () fait
        l = list();
        pour i allant de 0 a self.taille fait
            !l.debut(!self.en(i))
        fin
        renvoie l;
    fin

    methode destructeur () fait
        to_d = self.head.next;
        si (to_d.content == "fin-de-liste") fait
            libere self.head;
            libere self.tail;
            renvoie;
        fin
        tant que to_d.next.content != self.tail.content fait
            p = to_d;
            to_d = to_d.next;
            libere p;
        fin
        libere self.head;
    fin
fin
