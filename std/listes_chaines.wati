classe <type> noeud contient
    <type> contenu;
    <*noeud<type>> proch;

    methode <*noeud <type>> constructeur(<*noeud <type>> soit, <type> x) fait
        soit = <*noeud<type>>(!mallom(!taillede(<noeud<type>>)));
        soit.contenu = x;
        soit.proch = soit;
        renvoie soit;
    fin
    
    methode <*noeud <type>> constructeur(<*noeud <type>> soit) fait
        soit = <*noeud<type>>(!mallom(!taillede(<noeud<type>>)));
        soit.proch = soit;
        renvoie soit;
    fin

    methode <rien> pousse(<*noeud <type>> soit, <type> x) fait 
        si soit.proch == soit fait 
            soit.proch = <type>noeud(x);
        fin sinon fait 
            !soit.proch.pousse(x);
        fin
    fin

    methode <rien> print(<*noeud <type>> soit) fait 
        si soit.proch == soit fait 
            si !typede(soit.contenu) == "chaine" || !typede(soit.contenu) == "liste[chr]" fait
                !print('"'); !print(soit.contenu); !print('"');
            fin sinonsi !typede(soit.contenu) == "chr" fait 
                !print("'"); !print(soit.contenu); !print("'");
            fin sinon fait 
                !print(soit.contenu);
            fin
        fin sinon fait 
            si !typede(soit.contenu) == "chaine" || !typede(soit.contenu) == "liste[chr]" fait
                !print('"'); !print(soit.contenu); !print('"');
            fin sinonsi !typede(soit.contenu) == "chr" fait 
                !print("'"); !print(soit.contenu); !print("'");
            fin sinon fait 
                !print(soit.contenu);
            fin
            !print(", ");
            !soit.proch.print();
        fin
    fin
    
    methode <*noeud<type>> index(<*noeud <type>> soit, <ent> index) fait 
        si index == 0 fait
            renvoie soit;
        fin
        renvoie !soit.proch.index(index - 1);
    fin
    
    methode <type> pop(<*noeud <type>> soit, <ent> index) fait 
        si index == 1 fait
            p = soit.proch;
            c = p.contenu;
            si p.proch == p fait 
                soit.proch = soit;
            fin sinon fait 
                soit.proch = p.proch;
            fin
            !libere(<*rien>p);
            renvoie c;
        fin
        renvoie !soit.proch.pop(index - 1);
    fin
fin

classe <type> liste IT contient
    <*noeud<type>> debut;
    <ent> taille;
    <ent> dernier_index;
    <*noeud<type>> dernier_noeud;
    <*noeud<type>> iter;

    methode <*liste<type>> constructeur(<*liste<type>> soit) fait 
        soit = <*liste<type>>(!mallom(!taillede(<liste<type>>)));
        soit.debut = <type>noeud();
        soit.dernier_noeud = soit.debut;
        soit.dernier_index = 0;
        soit.taille = 0;
        renvoie soit;
    fin
    
    methode <rien> constructeur_pile(<*liste<type>> soit) fait 
        soit.debut = <type>noeud();
        soit.dernier_noeud = soit.debut;
        soit.dernier_index = 0;
        soit.taille = 0;
    fin

    methode <rien> ajoute(<*liste<type>> soit, <type> x) fait 
        !soit.debut.pousse(x);
        soit.taille = soit.taille + 1;
    fin

    methode <rien> print(<*liste<type>> soit) fait 
        si soit.taille == 0 fait !print("[ ]"); renvoie; fin
        !print("[ ");
        !soit.debut.proch.print();
        !print(" ]");
    fin

    methode <rien> println(<*liste<type>> soit) fait 
        !soit.print();
        !print('\n');
    fin
    
    methode <type> index (<*liste<type>> soit, <ent> index) fait 
        si index >= soit.taille fait 
            !erreur("!liste.index : L'index est plus grand que la taille de la liste");
        fin
        si index >= soit.dernier_index fait 
            p = !soit.dernier_noeud.index(index - soit.dernier_index + 1);
            dernier_index = index;
            dernier_noeud = p;
            renvoie p.contenu;
        fin
        <*noeud<type>> p = !soit.debut.index(index + 1);
        dernier_index = index;
        dernier_noeud = p;
        renvoie p.contenu;
    fin

    methode <rien> assigne_index(<*liste<type>> soit, <ent> index, <type> x) fait 
        si index >= soit.taille fait 
            !erreur("!liste.assigne_index : L'index est plus grand que la taille de la liste");
        fin
        si index >= soit.dernier_index fait 
            p = !soit.dernier_noeud.index(index - soit.dernier_index);
            p.contenu = x; // On assigne la valeur
            dernier_index = index;
            dernier_noeud = p;
        fin
        <*noeud<type>> p = !soit.debut.index(index + 1);
        p.contenu = x; // On assigne la valeur
        dernier_index = index;
        dernier_noeud = p;
    fin

    methode <type> pop (<*liste<type>> soit, <ent> index) fait 
        si index >= soit.taille fait 
            !erreur("!liste.pop : L'index est plus grand que la taille de la liste");
        fin
        soit.taille = soit.taille - 1;
        renvoie !soit.debut.pop(index + 1);
    fin

    methode <rien> debut (<*liste<type>> soit) fait 
        soit.iter = soit.debut;
    fin

    methode <type> suivant (<*liste<type>> soit) fait 
        soit.iter = soit.iter.proch;
        renvoie soit.iter.contenu;
    fin

    methode <bool> fin (<*liste<type>> soit) fait
        renvoie soit.iter.proch == soit.iter || soit.iter == 0;
    fin
fin