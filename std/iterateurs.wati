inclue "mallom.watiH";
inclue "io.watiH";

// l'itérateur le plus basique, génère un interval [a, b] de naturels
classe plage IT contient 
    <ent> depart;
    <ent> arrivee;
    <ent> courant;

    methode <*plage> constructeur (<*plage> soit, <ent> dep, <ent> arr) fait 
        soit = <*plage>(!mallom(!taillede(<plage>)));
        soit.depart = dep;
        soit.arrivee = arr;
        soit.courant = dep;
        renvoie soit;
    fin
    
    methode <rien> constructeur_pile (<*plage> soit, <ent> dep, <ent> arr) fait 
        soit.depart = dep;
        soit.arrivee = arr;
        soit.courant = dep;
    fin
    
    methode <*plage> constructeur (<*plage> soit, <ent> arr) fait 
        soit = <*plage>(!mallom(!taillede(<plage>)));
        soit.depart = 0;
        soit.arrivee = arr;
        soit.courant = 0;
        renvoie soit;
    fin
    
    methode <rien> constructeur_pile (<*plage> soit, <ent> arr) fait 
        soit.depart = 0;
        soit.arrivee = arr;
        soit.courant = 0;
    fin

    methode <*plage> debut (<*plage> soit) fait 
        soit.courant = soit.depart;
        renvoie soit;
    fin

    methode <bool> fin (<*plage> soit) fait
        renvoie soit.courant == soit.arrivee; 
    fin

    methode <ent> suivant (<*plage> soit) fait 
        c = soit.courant;
        soit.courant = soit.courant + 1;
        renvoie c;
    fin
fin