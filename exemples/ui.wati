inclue "./lib/std.wati";
inclue "./w_graphs/wati_graphs_events.wati";

classe bouton contient 
    methode constructeur (text, action, x, y, w, h) fait
        self.text = text;
        self.action = action;
        self.x = x;
        self.y = y;
        self.w = w;
        self.h = h;
        self.font = "font.ttf";

        self.hovered = 0;
    fin

    methode affiche () fait
        si self.hovered alors
            !set_color(90, 90, 90, 255)
        sinon
            !set_color(148, 148, 148, 255)
        fin
        !fill_rect(self.x, self.y, self.w, self.h)
        !draw_text(self.x + 2, self.y + 2, self.h - 4, self.w - 4, self.text, self.font, 10)
    fin

    methode survole (x, y, click) fait
        si !et(!et(self.x < x, self.x + self.w > x), !et(self.y < y, self.y + self.h > y)) alors
            self.hovered = 1;
            si click alors
                !self.action(self)
            fin
            renvoie 0;
        fin
        self.hovered = 0;
        renvoie 0;
    fin

    methode en_string () fait
        renvoie "<bouton text=" + self.text + ">";
    fin
fin


classe etiquette contient
    methode constructeur (text, x, y, w, h) fait
        self.x = x;
        self.y = y;
        self.w = w;
        self.h = h;
        self.text = text;
        self.font = "font.ttf";
    fin

    methode affiche () fait
        !draw_text(self.x, self.y, self.h, self.w, self.text, self.font, 10)
    fin

    methode survole (x, y, click) fait
        renvoie 0;
    fin

    methode en_string () fait
        renvoie "<etiquette text=" + self.text + ">";
    fin
fin

classe fenetre contient
    methode constructeur (w, h) fait
        !init_window(w, h)
        self.widgs = list();
        self.bindings = dictionnaire();
        self.click = 0;
    fin 

    methode demmare () fait
        !mainloop(!self.loop)
    fin

    methode bind (event, fn) fait
        si (!type(fn) != "fonction") alors
            !erreur("ne peut pas associer un Ã©venement avec le type '" + !type(fn) + "', ne peut que associer avec le type 'fonction'")
        fin
        !self.bindings.associe(event, fn)
    fin

    methode loop (events) fait
        !clear()
        l = !get_mouse_pos();
        Mx = l[0];
        My = l[1];
        pour e allant de 0 a events.taille fait
            event = events[e];
            si event == "MouseButtonDown" alors
                self.click = 1;
            sinonsi event == "MouseButtonUp" alors
                self.click = 0;
            fin

            r = !self.bindings.trouve_sans_erreur(event);
            si r != "clef_non_trouve" alors
                !r(event)
            fin
        fin

        pour i allant de 0 a self.widgs.taille fait
            widget = self.widgs[i];
            self.click = !widget.survole(Mx, My, self.click);
            !widget.affiche()
        fin
        !render()
        !set_color(0, 0, 0, 255)
        renvoie 1;
    fin
fin

fonction quitte_tout (e) fait
    !quit_window()
fin

win = fenetre(200, 200);
but = bouton("salut", !println, 10, 10, 50, 40);
!win.widgs.plus(but)
but = etiquette("tout le monde", 10, 60, 90, 50);
!win.widgs.plus(but)
!win.bind(Event("Keydown", "Escape"), !quit_window)
!win.demmare()